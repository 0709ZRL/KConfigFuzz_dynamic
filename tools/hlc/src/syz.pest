Root = { SOI ~ ( Include | Define | IncDir |
                 Resource | TypeDef | Struct | Flags |
                 Syscall | NEWLINE
                 )* 
        ~ EOI }

Syscall = {(!(KeyWords ~ " ") ~ &(Ident ~ LParen)) ~ Ident ~ LParen ~ Params? ~ RParen ~ Type? ~ SyscallAttr?}
SyscallAttr = {LParen ~ Type ~ (Comma ~ Type)* ~ RParen}
Params = {Param ~ (Comma ~ Param)*}
Param = {Ident ~ Type}

Flags = {!(KeyWords ~ " ") ~ &(Ident ~ Eq) ~ (IntFlags | StringFlags)}
IntFlags = {&(Ident ~ Eq ~ IntegerLiteral) 
                    ~ Ident ~ Eq ~ IntegerLiteral ~ (Comma ~ (IntegerLiteral))*}
StringFlags = {&(Ident ~ Eq ~ (StringLiteral | HexStringLiteral)) 
                    ~ Ident ~ Eq ~ (StringLiteral | HexStringLiteral) ~ (Comma ~ (StringLiteral | HexStringLiteral))*}

Struct = {!(KeyWords ~ " ") ~ &(Ident ~ (LBrace | LBrack)) ~ Ident ~ (StructBody | UnionBody)}
StructBody = {LBrace ~ NEWLINE ~ Fields  ~ RBrace ~ Attrs?} 
UnionBody = {LBrack ~ NEWLINE ~ Fields ~ RBrack ~ Attrs?} 
Attrs = {LBrack ~ Type ~ (Comma ~ Type)* ~ RBrack}
Fields = {NoneSlientNewLine* ~ Field ~ (NoneSlientNewLine* ~ Field)* ~ NEWLINE*}
Field = {Ident ~ Type ~ (LParen ~ Type ~ (Comma ~ Type)* ~ RParen)? ~ NEWLINE}

Resource = {ResourceKW ~ Ident ~ LBrack ~ Type ~ RBrack ~ (Colon ~ IntegerLiteral ~ (Comma ~ IntegerLiteral)*)?}

TypeDef = {TypeDefKw ~ Ident ~ (Template | Type)}
Template = {LBrack ~ Ident ~ (Comma ~ Ident)* ~ RBrack ~ (StructBody | UnionBody | Type)}

Type = { (Integer | CharLiteral | Ident | StringLiteral | HexStringLiteral) ~ (Colon ~ IntegerLiteral)* ~ TypeArgs?}
TypeArgs = {LBrack ~ Type ~ (Comma ~ Type)* ~ RBrack}

IncDir = {IncDirKW ~ StringLiteral}
Include = {IncludeKW ~ StringLiteral}
Define = {DefineKW ~ Ident ~ CExpr}

CExpr = @{(!("\n"| "\r") ~ ANY)*}
StringLiteral = @{("<" ~ (!("\n" | ">") ~ ANY)* ~ ">") | ("\"" ~ (!("\n" | "\"") ~ ANY)* ~ "\"")}
HexStringLiteral = @{ "`" ~  ASCII_HEX_DIGIT* ~ "`"}
IntegerLiteral = {Integer | Ident | CharLiteral}
CharLiteral = @{"'" ~ !("'") ~ ANY ~ "'"}
Integer = @{ ("0x" ~ ASCII_HEX_DIGIT+) | ("-"? ~ ASCII_DIGIT+ )} // match hex integer first.
Ident = @{(ASCII_ALPHANUMERIC | "_" | "$")+}

KeyWords = _{IncDir | Include | Define | Resource | TypeDefKw } 
IncDirKW = _{"incdir"}
IncludeKW = _{"include"}
DefineKW = _{"define"}
ResourceKW = _{"resource"}
TypeDefKw = _{"type"}

LParen = _{"("}
RParen = _{")"}
LBrack = _{"["}
RBrack = _{"]"}
LBrace = _{"{"}
RBrace = _{"}"}
Eq = _{"="}
Comma = _{","}
Colon = _{":"}

NoneSlientNewLine = {"\n"}
COMMENT = _{"#" ~ (!NEWLINE ~ ANY)*}
NEWLINE = _{"\n"}
WHITESPACE = _{" " | "\t" | "\r"}